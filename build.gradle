plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ru.safronov'
version = '0.2.0'
description = 'JobSearch'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

def deployDir = file("${projectDir}/deploy")

def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def dockerPrefix = isWindows ? ["cmd", "/c", "docker"] : ["docker"]

def composeBase = ["compose"]
def composeLocalFiles = [
        "-f", "docker-compose.yml",
        "-f", "docker-compose.build.yml",
        "-f", "docker-compose.perf-limits.yml"
]

tasks.register("composeUpLocal", Exec) {
    group = "docker"
    description = "Start stack locally (docker-compose.yml + docker-compose.build.yml)"
    workingDir = deployDir
    commandLine dockerPrefix + composeBase + [
            "-f", "docker-compose.yml",
            "-f", "docker-compose.build.yml",
            "up", "-d", "--build"
    ]
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("composeUpLocalLimits", Exec) {
    group = "docker"
    description = "Start stack locally with perf limits (docker-compose.yml + build + perf-limits)"
    workingDir = deployDir
    commandLine dockerPrefix + composeBase + composeLocalFiles + ["up", "-d", "--build"]
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("composeDown", Exec) {
    group = "docker"
    description = "Stop stack and remove volumes"
    workingDir = deployDir
    commandLine dockerPrefix + composeBase + ["down", "-v"]
    standardOutput = System.out
    errorOutput = System.err
}

def k6Cmd = { String containerScriptPath ->
    dockerPrefix + composeBase + composeLocalFiles + [
            "--profile", "loadtest",
            "run", "--rm",
            "k6",
            "run",
            "--out", "experimental-prometheus-rw",
            containerScriptPath
    ]
}

tasks.register("k6Smoke", Exec) {
    group = "k6"
    description = "Run k6 smoke test (local compose with build+limits)"
    workingDir = deployDir
    commandLine k6Cmd("perf/k6/smoke.js")
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("k6Load", Exec) {
    group = "k6"
    description = "Run k6 load test (local compose with build+limits)"
    workingDir = deployDir
    commandLine k6Cmd("perf/k6/load.js")
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("k6Stress", Exec) {
    group = "k6"
    description = "Run k6 stress test (local compose with build+limits)"
    workingDir = deployDir
    commandLine k6Cmd("perf/k6/stress.js")
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register("perfLoadLocal") {
    group = "k6"
    description = "Up (local build + limits) -> run load -> keep stack running"
    dependsOn "composeUpLocalLimits"
    dependsOn "k6Load"
}

tasks.register("perfLoadLocalClean") {
    group = "k6"
    description = "Up (local build + limits) -> run load -> down -v"
    dependsOn "composeUpLocalLimits"
    dependsOn "k6Load"
    finalizedBy "composeDown"
}
